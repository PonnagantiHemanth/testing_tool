// -------------------------------------
// Pipeline Configuration Section
properties([
    parameters([
        string(name: 'PROJECT_NAME', defaultValue: 'yoko_tp', description: 'firmware hex file name', ),
        string(name: 'BRANCH_NAME', defaultValue: 'rbt01_yoko', description: 'project branch name on goldenpass', ),
        string(name: 'COMPATIBLE_TAGS', defaultValue: '',
            description: 'previous versions to be compatible with (up to 3 versions)', ),
        string(name: 'CODE_TAG', defaultValue: '', description: 'optional tag or commitId on goldenpass', ),
        string(name: 'CODE_PATCHSET', defaultValue: '', description: 'optional patchset on goldenpass', ),
        string(name: 'SOC_NAME', defaultValue: 'MESON', description: 'System on a Chip definition', ),
        string(name: 'TEST_CONFIG', defaultValue: 'MESON/YOKO_TP', description: 'PRODUCT pyTestBox test configuration', ),
        string(name: 'PYTESTBOX_BRANCH_NAME', defaultValue: '',
            description: 'pyTestBox repo branch name', ),
        string(name: 'SITE_LABEL', defaultValue: 'twn', description: 'label to select node on a particular site', ),
    ]),
// End of Pipeline Configuration Section
// -------------------------------------
    pipelineTriggers([
        // Trigger on every update on master
        gerrit(
            serverName: 'goldenpass',
            gerritProjects: [[
                compareType: 'PLAIN',
                pattern: "ccp_fw/${params.BRANCH_NAME}",
                branches: [[ compareType: 'PLAIN', pattern: 'master' ]],
                disableStrictForbiddenFileVerification: false
            ]],
            triggerOnEvents: [
                refUpdated(),
            ]
        )
    ]),
])

timestamps {

    node ('builder_ci') {
        stage ('Build Job - Checkout') {
            if (env.GERRIT_CHANGE_SUBJECT && env.GERRIT_CHANGE_OWNER_NAME) {
                echo "ChangeLog: ${env.GERRIT_CHANGE_SUBJECT} from ${env.GERRIT_CHANGE_OWNER_NAME}"
            }
            if (env.GERRIT_REFSPEC) {
                ref_spec = env.GERRIT_REFSPEC
                branch_name = env.GERRIT_PATCHSET_REVISION
            }
            else
            {
                ref_spec = '+refs/heads/*:refs/remotes/origin/*'
                branch_name = '*/master'
            }
            echo "Refspec for checkout: ${ref_spec}"
            checkout([$class: 'GitSCM',
                branches: [[name: "${branch_name}"]], doGenerateSubmoduleConfigurations: false,
                extensions: [[ $class: 'CleanCheckout'],
                    [$class: 'SubmoduleOption', disableSubmodules: false, parentCredentials: false,
                    recursiveSubmodules: true, reference: '', trackingSubmodules: false
                ]],
                submoduleCfg: [],
                userRemoteConfigs: [[credentialsId: 'Jenkins user',
                    url: "ssh://Jenkins@goldenpass.logitech.com:29418/ccp_fw/${params.BRANCH_NAME}",
                    refspec: "${ref_spec}"
                ]]
            ])
        } // End of stage ('Build Job - Checkout')

        stage ('Build Job - Build') {
            echo "PROJECT_NAME = ${params.PROJECT_NAME}"
            if (params.CODE_PATCHSET != '') {
              // Shell build step - sync on a patchset
              sh "git fetch ssh://Jenkins@goldenpass.logitech.com:29418/ccp_fw/${params.BRANCH_NAME} refs/changes/${params.CODE_PATCHSET}"
              sh "git checkout FETCH_HEAD"
            } else {
              if (params.CODE_TAG != '') {
                // Shell build step - sync on a tag
                sh "git checkout ${params.CODE_TAG}"
              }
            }
            sh "git submodule update -i"
            sh "git clean -x -f"
            // Shell build step - Copy hard-coded Jenkins dfu key into dfu test key
            // Shell build step - Build production and test firmwares,
            //                    valid test dfu and phcs#1 v1.5 verification dfu files
            // Shell build step - Build previous firmware versions based on the compatible TAG list
            sh """
              cd gcc_eclipse
              make clean PROJECT=${params.PROJECT_NAME}
              make clean PROJECT=${params.PROJECT_NAME} OUT_DIR_PFX=build_ci
              cp ../config/priv_tstkey_jenkins.pem ../config/priv_tstkey.pem
              mkdir -p build_ci_${params.PROJECT_NAME}
              make -j8 BUILD_CONFIG=PROD PROJECT=${params.PROJECT_NAME} >> build_ci_${params.PROJECT_NAME}/${params.PROJECT_NAME}_build.log
              CPPFLAGS=-DCI_BUILD=1 make -j8 PROJECT=${params.PROJECT_NAME} OUT_DIR_PFX=build_ci >> build_ci_${params.PROJECT_NAME}/${params.PROJECT_NAME}_build.log
              CPPFLAGS=-DCI_BUILD=1 make -j8 BUILD_CONFIG=PROD PROJECT=${params.PROJECT_NAME} OUT_DIR_PFX=build_ci dfu_evt >> build_ci_${params.PROJECT_NAME}/${params.PROJECT_NAME}_build.log

              for tag in ${params.COMPATIBLE_TAGS}; do
                  git checkout tags/\${tag}
                  git submodule update -i
                  git clean -x -f
                  make clean PROJECT=${params.PROJECT_NAME} OUT_DIR_PFX=tag_\${tag}
                  cp ../config/priv_tstkey_jenkins.pem ../config/priv_tstkey.pem
                  CPPFLAGS=-DCI_BUILD=1 make -j8 BUILD_CONFIG=PROD PROJECT=${params.PROJECT_NAME} OUT_DIR_PFX=tag_\${tag} >> build_ci_${params.PROJECT_NAME}/${params.PROJECT_NAME}_build.log
                  cd tag_\${tag}_${params.PROJECT_NAME}
                  mv ${params.PROJECT_NAME}_prod.hex ../build_ci_${params.PROJECT_NAME}/${params.PROJECT_NAME}_prod_\${tag}.hex
                  cd ..
              done
            """
            sh(returnStdout: true, script: '''#!/bin/bash
              if [ ! -z "$(git status --porcelain)" ]; then
                echo "Uncommitted changes in tracked files"
                echo "$(git status --porcelain)"
                exit 1
              fi
            '''.stripIndent())
            archiveArtifacts allowEmptyArchive: false,
                             artifacts: """**gcc_eclipse/**/*.dfu,
                                           **gcc_eclipse/**/${params.PROJECT_NAME}_build.log,
                                           **gcc_eclipse/build_*/${params.PROJECT_NAME}*.hex,
                                           **gcc_eclipse/build_ci_*prod/${params.PROJECT_NAME}*.elf,
                                           **/dfu_*_key.txt,
                                           **/priv_tstkey_jenkins.pem,
                                           **gcc_eclipse/build_ci_*prod/*.map""",
                             caseSensitive: true,
                             defaultExcludes: true,
                             fingerprint: false,
                             onlyIfSuccessful: false
            // Clean workspace at build end
            cleanWs()
        } // End of stage ('Build Job - Build')
    } // End of node ('builder')

    node ('fw_check') {

        stage ('Firmwares Comparison Job - Checkout') {
            checkout([$class: 'GitSCM',
                branches: [[name: "${params.PYTESTBOX_BRANCH_NAME}"]], doGenerateSubmoduleConfigurations: false,
                extensions: [[$class: 'CleanCheckout'],
                    [$class: 'PruneStaleBranch'], pruneTags(true),
                    [$class: 'SubmoduleOption',
                    disableSubmodules: false,
                    parentCredentials: true,
                    recursiveSubmodules: true,
                    reference: '',
                    trackingSubmodules: false
                ]],
                submoduleCfg: [],
                userRemoteConfigs: [[credentialsId: 'GithubProductsCIToken', url: "https://github.com/Logitech/py-test-box.git"]]
            ])
        } // End of stage ('Firmwares Comparison Job - Checkout')

        stage ('Firmwares Comparison Job - Build') {
            script {
                copyArtifacts(
                    filter: """**gcc_eclipse/build_*/${params.PROJECT_NAME}_prod.hex,
                               **gcc_eclipse/build_ci_*prod/${params.PROJECT_NAME}*.elf""",
                    projectName: env.JOB_NAME,
                    fingerprintArtifacts: true,
                    selector: specific(env.BUILD_NUMBER),
                    target: 'PYTESTBOX/TESTS/DFU_FILES'
                )
                try {
                    // Shell Unit Test step - framework initialization
                    sh """
                      python3 PYTESTBOX/LIBS/PYSETUP/pyEnvChecker.py -q
                    """
                    // Shell Unit Test step - production vs test firmwares comparison
                    def path = 'PYTESTBOX/TESTS/DFU_FILES/gcc_eclipse/build_'
                    sh """
                      python3 PYTESTBOX/CICD/ciscripts/compare_hex.py  \
                          -f ${path}${params.PROJECT_NAME}/${params.PROJECT_NAME}_prod.hex  \
                          -c ${path}ci_${params.PROJECT_NAME}/${params.PROJECT_NAME}_prod.hex \
                          -b ${path}ci_${params.PROJECT_NAME}_btldr_prod/${params.PROJECT_NAME}_btldr.elf \
                          -e ${path}ci_${params.PROJECT_NAME}_app_prod/${params.PROJECT_NAME}_app.elf
                    """
                }
                finally {
                    try {
                        // Permission cleanup
                        sh "python3 PYTESTBOX/CICD/ciscripts/permissions_clean_up.py"
                    }
                    catch(all) {
                        echo 'Error cleaning up permissions'
                    }
                }
            }
        } // End of stage ('Firmwares Comparison Job - Build')
    } // End of node ('fw_check')

    node (label: "${params.PROJECT_NAME} && ${params.SITE_LABEL} && power_board_v3") {
        stage ('Test Execution Job - Checkout') {
            checkout(
                [$class: 'GitSCM',
                    branches: [[name: "${params.PYTESTBOX_BRANCH_NAME}"]],
                    doGenerateSubmoduleConfigurations: false,
                    extensions: scm.extensions + [[$class: 'CleanCheckout'],
                        [$class: 'PruneStaleBranch'], pruneTags(true),
                        [$class: 'SubmoduleOption',
                        disableSubmodules: false,
                        parentCredentials: true,
                        recursiveSubmodules: true,
                        reference: '',
                        trackingSubmodules: false
                    ]],
                    submoduleCfg: [],
                    userRemoteConfigs: [
                     [credentialsId: 'GithubProductsCIToken',
                      url: "https://github.com/Logitech/py-test-box.git"]
                    ]
                ]
            )
        } // End of stage ('Test Execution Job - Checkout')

        stage ('Test Execution Job - Build') {
            script {
                copyArtifacts(
                    filter: """**gcc_eclipse/**/*.dfu,
                               **gcc_eclipse/build_ci_*/${params.PROJECT_NAME}_prod*.hex,
                               **/${params.PROJECT_NAME}*.elf,
                               **/dfu_${params.PROJECT_NAME}_key.txt,
                               **/priv_tstkey_jenkins.pem""",
                    projectName: env.JOB_NAME,
                    fingerprintArtifacts: true,
                    selector: specific(env.BUILD_NUMBER),
                    target: 'PYTESTBOX/TESTS/DFU_FILES',
                    flatten: true
                )

                // Shell build step - framework initialization
                sh """
                python3 PYTESTBOX/LIBS/PYSETUP/pyEnvChecker.py -q
                """
                // Get Node Name
                def node_name = "${NODE_NAME}"
                clean_node_name = node_name.replaceAll("\\s","_")
                echo "The Node Name is: ${clean_node_name}"
                // Shell Integration Test step - voltage calibration
                sh """
                python3 PYTESTBOX/CICD/ciscripts/power_supply.py -i -g -c -e
                """
                // Shell Integration Test step - turn on power
                sh """
                python3 PYTESTBOX/CICD/ciscripts/power_supply.py -i -s True -v 4.2 -o
                """
                // Product settings path
                def settings_path = "PYTESTBOX/TESTS/SETTINGS/PYTESTBOX/PRODUCT"
                // Shell Integration Test step - firmware code loading
                sh """
                python3 PYTESTBOX/CICD/ciscripts/jlink.py -v -e \
                    -f PYTESTBOX/TESTS/DFU_FILES/${params.PROJECT_NAME}_prod.hex  \
                    -p ${params.SOC_NAME}  \
                    -n ${settings_path}/${params.TEST_CONFIG}/nvs_uicr_${params.PROJECT_NAME}_${clean_node_name}.hex
                """
                // Shell Integration Test step - power supply GPIO cleanup
                sh """
                python3 PYTESTBOX/CICD/ciscripts/power_supply.py -e
                """

                if (currentBuild.currentResult != 'SUCCESS')
                {
                    echo "Test node setup stage failed"
                    // The exit was commented out as the test setup on pollux fails at the battery calibration step,
                    // which does not cause problems for the rest of the testsuite
                    // sh "exit 1"
                }

                try {
                    timeout(time: 60, unit: 'MINUTES', activity: true) {
                        // Shell Integration Test step - tests execution
                        sh """
                            python3 PYTESTBOX/TESTS/main.py  \
                                --file=PYTESTBOX/TESTS/SETTINGS/testFilter.txt \
                                --variant=PRODUCT/${params.TEST_CONFIG} \
                                --product=PYTESTBOX \
                                --target=DEVICE \
                                --output=console,log,junit,jrl \
                                --level=Interface,Business,Functionality,ErrorHandling,Robustness,Security,Time-consuming \
                                --quiet
                        """
                    }
                }
                finally {
                    try {
                        // Save artifacts
                        archiveArtifacts allowEmptyArchive: true,
                           artifacts: """**/LOCAL/**/${params.TEST_CONFIG}/**/log/*.log,
                                         **/LIBS/LOCAL/Local.txt""",
                           caseSensitive: true, defaultExcludes: true, fingerprint: false, onlyIfSuccessful: false
                    }
                    catch(all) {
                        echo 'Error saving artifacts'
                    }
                }

                // Save JUnit Results
                junit "**/LOCAL/**/${params.TEST_CONFIG}/**/*.xml"
            }
        } // End of stage ('Test Execution Job - Build')
    } // End of node (label: "${params.PROJECT_NAME}")
} // End of timestamps
